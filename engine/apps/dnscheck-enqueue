#!/usr/bin/env perl

use 5.10.1;
use strict;
use warnings;

use DNSCheck;
use Getopt::Long;

my $inputfile   = undef;
my $source_name = 'dnscheck-enqueue';
my $priority    = 5;
my $help        = 0;

GetOptions(
    'source=s'   => \$source_name,
    'priority=i' => \$priority,
    'file=s'     => \$inputfile,
    'help!'      => \$help,
);

if ( $help ) {
    say "usage: $0 [--source=<name>] [--priority=<int>] [--file=<filename>] domain domain domain ...";
    exit( 2 );
}

my $dc  = DNSCheck->new;
my $dbh = $dc->dbh;

if ( not $dbh ) {
    say 'Failed to connect to database.';
    exit( 1 );
}

# Get source id
$dbh->do( q[INSERT IGNORE INTO source (name) VALUES (?)], undef, $source_name );
my ( $source_id ) = $dbh->selectrow_array( q[SELECT id FROM source WHERE name = ?], undef, $source_name );

my $sth = $dbh->prepare( q[INSERT INTO queue (domain, priority, source_id) VALUES (?,?,?)] );
my $reg_ins = $dbh->prepare(q[INSERT INTO registry (domain, registrar, age, source_id, registration_id) VALUES (?,?,?,?,?)]);

if (defined $inputfile && -f $inputfile) {
    # Explicitly disable AutoCommit, to avoid commit after each line
    $dbh->{AutoCommit} = 0;
    open(F, "<", "$inputfile") or die "Can't open given input file: $!\n";
    while(<F>) {
        chomp;
        my ($domain, $reg_id, $age, $registration_id) = split /\|/;
        $sth->execute($domain, $priority, $source_id);
        $reg_ins->execute($domain, $reg_id, $age, $source_id, $registration_id);
        # Commit every 1000 inserts
        if ($. % 1000 == 0) {
            $dbh->commit;
            print "Loaded $. domains\r";
        }
    }
    print "\n";
    close F;
    $dbh->commit;
}
else {
    foreach my $domain ( @ARGV ) {
        $sth->execute( $domain, $priority, $source_id );
    }
}
